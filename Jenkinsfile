def setStatus(String state, String description) {
    // Shell expands $GITHUB_TOKEN so the secret is not interpolated by Groovy
    sh """
    curl -L \\
    -X POST \\
    -H "Accept: application/vnd.github+json" \\
    -H "Authorization: Bearer \$GITHUB_TOKEN" \\
    -H "X-GitHub-Api-Version: 2022-11-28" \\
    https://api.github.com/repos/tanndlin/MovieHost/statuses/\$GIT_COMMIT \\
    -d '{"state":"${state}","description":"${description}","context":"Jenkins"}'
    """
}

pipeline {
    agent any

    environment {
        GITHUB_TOKEN = credentials('GITHUB_TOKEN')
        DOCKER_VOLS = '-v jenkins_jenkins_home:/var/jenkins_home -v cargo-registry-cache:/usr/local/cargo/registry -v npm-cache:/root/.npm'
        NODE_IMAGE = 'node:22'
        RUST_IMAGE = 'moviehost-rust-ci:1.97'
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
                setStatus('pending', 'Build in progress')
            }
        }

        // Frontend and backend don't depend on each other, so run them side by side.
        // Within each, cheap checks run before the slow builds.
        stage('CI') {
            parallel {
                stage('Frontend') {
                    stages {
                        stage('Install Frontend') {
                            steps {
                                sh '''
                                docker run --rm $DOCKER_VOLS -w $WORKSPACE/frontend $NODE_IMAGE \
                                    sh -c "npm ci"
                                '''
                            }
                        }

                        stage('Format Frontend') {
                            steps {
                                catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                                    sh '''
                                    docker run --rm $DOCKER_VOLS -w $WORKSPACE/frontend $NODE_IMAGE \
                                        sh -c "npm run format:check"
                                    '''
                                }
                            }
                        }

                        // Fails if a handler changed without `npm run gen:api` being re-run.
                        stage('Check Generated API Types') {
                            steps {
                                sh '''
                                docker run --rm $DOCKER_VOLS -w $WORKSPACE/frontend $NODE_IMAGE \
                                    sh -c "npm run gen:api:check"
                                '''
                            }
                        }

                        stage('Lint Frontend') {
                            steps {
                                catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                                    sh '''
                                    docker run --rm $DOCKER_VOLS -w $WORKSPACE/frontend $NODE_IMAGE \
                                        sh -c "npm run lint"
                                    '''
                                }
                            }
                        }

                        stage('Build Frontend') {
                            steps {
                                sh '''
                                docker run --rm $DOCKER_VOLS -w $WORKSPACE/frontend $NODE_IMAGE \
                                    sh -c "npm run build"
                                '''
                            }
                        }
                    }
                }

                stage('Backend') {
                    stages {
                        stage('Prepare Rust Image') {
                            steps {
                                sh 'docker build -q -t $RUST_IMAGE - < server/ci.dockerfile'
                            }
                        }

                        stage('Format Backend') {
                            steps {
                                catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                                    sh '''
                                    docker run --rm $DOCKER_VOLS -w $WORKSPACE/server $RUST_IMAGE \
                                        sh -c "cargo fmt -- --check"
                                    '''
                                }
                            }
                        }

                        stage('Lint Backend') {
                            steps {
                                catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                                    sh '''
                                    docker run --rm $DOCKER_VOLS -w $WORKSPACE/server $RUST_IMAGE \
                                        sh -c "cargo clippy --all-targets -- -D clippy::pedantic"
                                    '''
                                }
                            }
                        }

                        stage('Build Backend') {
                            steps {
                                sh '''
                                docker run --rm $DOCKER_VOLS -w $WORKSPACE/server $RUST_IMAGE \
                                    sh -c "cargo build --release"
                                '''
                            }
                        }

                        stage('Test Backend') {
                            steps {
                                sh '''
                                docker run --rm $DOCKER_VOLS -w $WORKSPACE/server $RUST_IMAGE \
                                    sh -c "cargo test --release"
                                '''
                            }
                        }
                    }
                }
            }
        }

        stage('Deploy') {
            when {
                allOf {
                    expression { env.GIT_BRANCH == 'master' || env.GIT_BRANCH == 'origin/master' }
                    expression { currentBuild.currentResult == 'SUCCESS' }
                }
            }
            environment {
                MEDIA_HOME = credentials('MEDIA_HOME')
                TMDB_API_KEY = credentials('TMDB_API_KEY')
            }
            steps {
                sh 'docker compose -f docker-compose.yml -f docker-compose.deploy.yml -p moviehost up -d --build --remove-orphans'
            }
        }
    }

    post {
        success {
            setStatus('success', 'Build succeeded')
        }
        failure {
            setStatus('failure', 'Build failed')
        }
    }
}
